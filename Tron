/* BM Tron inspired game
 Made by Eren Sulutas
 04-06-2018
 */

int x1 = 100;
int y1 = 100;
int x2 = 1500;
int y2 = 1500;
char moving1 = 'r';
char moving2 = 'l';
boolean playUser = false;
boolean playBot = false;
int playMode;
boolean gameOver1 = false;
boolean gameOver2 = false;

void reset() {
  x1 = 100;
  y1 = 100;
  x2 = 1500;
  y2 = 1500;
  moving1 = 'r';
  moving2 = 'l';
  gameOver1 = false;
  gameOver2 = false;
  setup();
}

void setup() {
  size (1600, 1600);
  background(0);
  fill(13, 233, 255);
  rect(0, 0, width, 10); 
  noStroke();
  rect(width-10, 0, 10, height); 
  noStroke();
  rect(0, height-10, width, 10); 
  noStroke();
  rect(0, 0, 10, height); 
  noStroke();
}

void draw() {
  if (gameOver1 || gameOver2) {
    textAlign(CENTER);
    textSize(70);
    fill(255, 0, 0);
    text("GAME OVER", 800, 700);
    fill(255);
    textSize(30);
    text("Space to continue", 800, 800);

    if (gameOver1 && !gameOver2) { // Player 2 victory
      fill(255);
      textSize(60);
      text("Player 2 wins!", 800, 550);
    } else if (gameOver2 && !gameOver1) { // Player 1 victory 
      fill(255);
      textSize(60);
      text("Player 1 wins!", 800, 550);
    }
  } else if (playMode == 0) {
    textAlign(CENTER);
    textSize(70);
    fill(255);
    text("TRON", 800, 600);
    textSize(50);
    text("2 Players", 810, 700);
    text("Vs CPU", 810, 800);
    fill(13, 233, 255);
    rect(720, 610, 172, 4);
    noFill();
    rect(695, 650, 225, 70);
    rect(695, 750, 225, 70);
    stroke(13, 233, 255);
  } else if (playMode == 1) {
    player1();
    player2();
  } else if (playMode == 2) {
    player1();
    playerBot();
  }
}

void player1() {
  if (get(x1, y1) == color(0, 0, 0)) { // Checks if the light cycle has intersected with another trail
    fill(13, 233, 255);
    noStroke();
    rect(x1, y1, 10, 10);

    if (moving1 == 'u') {
      y1 -= 10;
    } else if (moving1 == 'd') {
      y1 += 10;
    } else if (moving1 == 'r') {
      x1 += 10;
    } else if (moving1 == 'l') {
      x1 -= 10;
    }
  } else {
    gameOver1 = true;
  }
}

void player2() {
  if (get(x2, y2) == color(0, 0, 0)) { // Checks if the light cycle has intersected with another trail
    fill(255, 196, 44);
    noStroke();
    rect(x2, y2, 10, 10);

    if (moving2 == 'u') {
      y2 -= 10;
    } else if (moving2 == 'd') {
      y2 += 10;
    } else if (moving2 == 'r') {
      x2 += 10;
    } else if (moving2 == 'l') {
      x2 -= 10;
    }
  } else {
    gameOver2 = true;
  }
}

void playerBot() {
  if (get(x2, y2) == color(0, 0, 0)) { // Checks if the light cycle has intersected with another trail
    fill(255, 196, 44);
    noStroke();
    rect(x2, y2, 10, 10);

    if (moving2 == 'u') {
      y2 -= 10;
      if (get(x2, y2 - 10) != color(0, 0, 0)) { // Moving up, goes to the side if there is an obstruction
        if (get(x2 - 10, y2) != color(0, 0, 0)) {
          moving2 = 'r';
        } else {
          moving2 = 'l';
        }
      }
    } else if (moving2 == 'd') {
      y2 += 10;
      if (get(x2, y2 + 10) != color(0, 0, 0)) { // Moving down, goes to the side if there is an obstruction
        if (get(x2 - 10, y2) != color(0, 0, 0)) {
          moving2 = 'r';
        } else {
          moving2 = 'l';
        }
      }
    } else if (moving2 == 'r') {
      x2 += 10;
      if (get(x2 + 10, y2) != color(0, 0, 0)) { // Moving right, goes up/down if there is an obstruction
        if (get(x2, y2 - 10) != color(0, 0, 0)) {
          moving2 = 'd';
        } else {
          moving2 = 'u';
        }
      }
    } else if (moving2 == 'l') {
      x2 -= 10;
      if (get(x2 - 10, y2) != color(0, 0, 0)) { // Moving left, goes up/down if there is an obstruction
        if (get(x2, y2 - 10) != color(0, 0, 0)) {
          moving2 = 'd';
        } else {
          moving2 = 'u';
        }
      }
    }
  } else {
    gameOver2 = true;
  }
}

void keyPressed() {
  // Player 1
  if (keyCode == 'W' && moving1 != 'd') { 
    moving1 = 'u';
  } else if (keyCode == 'S' && moving1 != 'u') {
    moving1 = 'd';
  } else if (keyCode == 'D' && moving1 != 'l') {
    moving1 = 'r';
  } else if (keyCode == 'A' && moving1 != 'r') {
    moving1 = 'l';
  }
  // Player 2
  if (playMode == 1) {
    if (keyCode == UP && moving2 != 'd') { 
      moving2 = 'u';
    } else if (keyCode == DOWN && moving2 != 'u') {
      moving2 = 'd';
    } else if (keyCode == RIGHT && moving2 != 'l') {
      moving2 = 'r';
    } else if (keyCode == LEFT && moving2 != 'r') {
      moving2 = 'l';
    }
  }
  //Restart
  if (keyCode == ' ') {
    reset();
  }
}

void mousePressed() {
  if (playerButton()) {
    playMode = 1;
    reset();
  } else if (botButton()) {
    playMode = 2;
    reset();
  }
}

boolean playerButton () { 
  if (mouseX >= 695 && mouseX <= 915 && mouseY >= 650 && mouseY <= 720) {
    return true;
  } else {
    return false;
  }
}

boolean botButton () { 
  if (mouseX >= 695 && mouseX <= 915 && mouseY >= 750 && mouseY <= 820) {
    return true;
  } else {
    return false;
  }
}
